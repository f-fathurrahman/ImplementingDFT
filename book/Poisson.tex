\chapter{Poisson equation}
\label{chap:poisson_3d}

\section{Conjugate gradient method}

In this section we will discuss a second type of equation
that is important in solving Kohn-Sham equation,
namely the Poisson equation. In the
context of solving Kohn-Sham equation, Poisson equation is used to
calculate classical electrostatic potential due to some electronic
charge density.
The Poisson equation that we will solve have the following form:
\begin{equation}
\nabla^2 V_{\mathrm{Ha}}(\mathbf{r}) = -4\pi\rho(\mathbf{r})
\label{eq:poisson_3d}
\end{equation}
where $\rho(\mathbf{r})$ is the electronic density. Using finite
difference discretization for the operator $\nabla^2$ we end up with
the following linear equation:
\begin{equation}
\mathbb{L} \mathbf{V} = \mathbf{f}
\label{eq:linear_eq_poisson}
\end{equation}
where $\mathbb{L}$ is the matrix representation of the Laplacian operator
$\mathbf{f}$ is the discrete representation of the right hand side of the equation
\ref{eq:poisson_3d}, and the unknown $\mathbf{V}$ is the discrete representation of
the Hartree potential.

There exist several methods for solving the linear equation \ref{eq:linear_eq_poisson}.
We will use the so-called conjugate gradient method for solving this equation.
This method is an iterative method, so it generally needs a good preconditioner to
achieve good convergence. A detailed derivation about the algorithm is beyond this
article and the readers are referred to several existing literatures \cite{Hestenes1952,Shewchuk1994}
and a webpage \cite{wiki-Conjugate-gradient} for more
information. The algorithm is described in \txtinline{Poisson_solve_PCG.jl}

\begin{juliacode}
function Poisson_solve_PCG( Lmat, prec, f; NiterMax=1000 TOL=5.e-10 )
  Npoints = size(f,1)
  phi = zeros( Float64, Npoints )
  r = zeros( Float64, Npoints )
  p = zeros( Float64, Npoints )
  z = zeros( Float64, Npoints )
  nabla2_phi = Lmat*phi
  r = f - nabla2_phi
  z = copy(r)
  ldiv!(prec, z)
  p = copy(z)
  rsold = dot( r, z )
  for iter = 1 : NiterMax
    nabla2_phi = Lmat*p
    alpha = rsold/dot( p, nabla2_phi )
    phi = phi + alpha * p
    r = r - alpha * nabla2_phi
    z = copy(r)
    ldiv!(prec, z)
    rsnew = dot(z, r)
    deltars = rsold - rsnew
    if sqrt(abs(rsnew)) < TOL
      break
    end
    p = z + (rsnew/rsold) * p
    rsold = rsnew
  end
  return phi
end
\end{juliacode}

To test our implementation we will adopt a problem given in Prof. Arias Practical
DFT mini-course \cite{practical-DFT-mini-course}.
In this problem we will solve Poisson equation for a given charge density built from
superposition of two Gaussian charge density:
\begin{equation}
\rho(\mathbf{r}) = \frac{1}{(2\pi\sigma_{1}^{2})^{\frac{3}{2}}} \exp\left( -\frac{\mathbf{r}^2}{2\sigma_{1}^{2}} \right)
- \frac{1}{(2\pi\sigma_{2}^{2})^{\frac{3}{2}}} \exp\left( -\frac{\mathbf{r}^2}{2\sigma_{2}^{2}} \right)
\end{equation}
After we obtain $V_{\mathrm{Ha}}(\mathbf{r})$, we calculate the Hartree energy:
\begin{equation}
E_{\mathrm{Ha}} = \frac{1}{2} \int \rho(\mathbf{r}) V_{\mathrm{Ha}}(\mathbf{r})\,\mathrm{d}\mathbf{r}
\end{equation}
and compare the result with the analytical formula.

\begin{juliacode}
function test_main( NN::Array{Int64} )
  AA = [0.0, 0.0, 0.0]
  BB = [16.0, 16.0, 16.0]
  # Initialize grid
  FD = FD3dGrid( NN, AA, BB )
  # Box dimensions
  Lx = BB[1] - AA[1]
  Ly = BB[2] - AA[2]
  Lz = BB[3] - AA[3]
  # Center of the box
  x0 = Lx/2.0
  y0 = Ly/2.0
  z0 = Lz/2.0
  # Parameters for two gaussian functions
  sigma1 = 0.75 
  sigma2 = 0.50

  Npoints = FD.Nx * FD.Ny * FD.Nz
  rho = zeros(Float64, Npoints)
  phi = zeros(Float64, Npoints)
  # Initialization of charge density
  dr = zeros(Float64,3)
  for ip in 1:Npoints
    dr[1] = FD.r[1,ip] - x0
    dr[2] = FD.r[2,ip] - y0
    dr[3] = FD.r[3,ip] - z0
    r = norm(dr)
    rho[ip] = exp( -r^2 / (2.0*sigma2^2) ) / (2.0*pi*sigma2^2)^1.5 -
              exp( -r^2 / (2.0*sigma1^2) ) / (2.0*pi*sigma1^2)^1.5
  end
  deltaV = FD.hx * FD.hy * FD.hz
  Laplacian3d = build_nabla2_matrix( FD, func_1d=build_D2_matrix_9pt )
  prec = aspreconditioner(ruge_stuben(Laplacian3d))
  @printf("Test norm charge: %18.10f\n", sum(rho)*deltaV)
  print("Solving Poisson equation:\n")
  phi = Poisson_solve_PCG( Laplacian3d, prec, -4*pi*rho, 1000, verbose=true, TOL=1e-10 )
  # Calculation of Hartree energy
  Unum = 0.5*sum( rho .* phi ) * deltaV
  Uana = ((1.0/sigma1 + 1.0/sigma2 )/2.0 - sqrt(2.0)/sqrt(sigma1^2 + sigma2^2))/sqrt(pi)
  @printf("Numeric  = %18.10f\n", Unum)
  @printf("Uana     = %18.10f\n", Uana)
  @printf("abs diff = %18.10e\n", abs(Unum-Uana))
end
test_main([64,64,64])
\end{juliacode}

Result:
\begin{textcode}
Numeric  =       0.0551434259
Uana     =       0.0551425277
abs diff =   8.9818466372e-07
\end{textcode}

FIXME: Needs correction, boundary condition is not treated properly.

\section{Reciprocal space method}

Another popular method for solving Poisson equation is by using
reciprocal space method.
This method is the natural choice for systems with periodic boundary condition.

In the reciprocal or $\mathbf{G}$-space, Poisson equation is transformed to
\begin{equation}
\mathbf{G}^{2} V_{\mathrm{Ha}}(\mathbf{G}) = 4\pi\rho(\mathbf{G})
\end{equation}
for which we can solve:
\begin{equation}
V_{\mathrm{Ha}}(\mathbf{G}) = 4\pi \frac{\rho(\mathbf{G})}{\mathbf{G}^{2}}
\label{eq:V_Ha_G}
\end{equation}
Note that, we must exclude the term $\mathbf{G}=\mathbf{0}$ in the Equation
\ref{eq:V_Ha_G}.

TODO: Move discussion about GVectors to Chapter about 3d Sch equation?
(periodic system)

The $\mathbf{G}$-vectors are defined as
\begin{equation}
\mathbf{G} = n_{1}\mathbf{b}_{1} + n_{2}\mathbf{b}_{2} + n_{3}\mathbf{b}_{3}
\end{equation}
where $b_{i} = \frac{2\pi}{a_{i}}$.

Initializing GVectors:
\begin{juliacode}
function GVectors( grid )
  Npoints = grid.Npoints
  Nx = grid.Nx; Ny = grid.Ny; Nz = grid.Nz
  Lx = grid.Lx; Ly = grid.Ly; Lz = grid.Lz
  RecVecs = zeros(3,3)
  RecVecs[1,1] = 2.0*pi/Lx
  RecVecs[2,2] = 2.0*pi/Ly
  RecVecs[3,3] = 2.0*pi/Lz
  Δ = max(grid.hx, grid.hy, grid.hz)
  ecutrho = (pi/Δ)^2
  Ns = (Nx,Ny,Nz)
  Ng = calc_Ng( Ns, RecVecs, ecutrho )
  G  = zeros(Float64,3,Ng)
  G2 = zeros(Float64,Ng)
  idx_g2r = zeros(Int64,Ng)
  ig = 0
  ip = 0
  for k in 0:Ns[3]-1, j in 0:Ns[2]-1, i in 0:Ns[1]-1
      ip = ip + 1
      gi = _flip_fft( i, Ns[1] )
      gj = _flip_fft( j, Ns[2] )
      gk = _flip_fft( k, Ns[3] )
      Gx = RecVecs[1,1]*gi + RecVecs[1,2]*gj + RecVecs[1,3]*gk
      Gy = RecVecs[2,1]*gi + RecVecs[2,2]*gj + RecVecs[2,3]*gk
      Gz = RecVecs[3,1]*gi + RecVecs[3,2]*gj + RecVecs[3,3]*gk
      G2_temp = Gx^2 + Gy^2 + Gz^2
      if 0.5*G2_temp <= ecutrho
          ig = ig + 1
          G[1,ig] = Gx
          G[2,ig] = Gy
          G[3,ig] = Gz
          G2[ig] = G2_temp
          idx_g2r[ig] = ip
      end
  end
  idx_sorted = sortperm(G2)
  G = G[:,idx_sorted]
  G2 = G2[idx_sorted]
  idx_g2r = idx_g2r[idx_sorted]
  G2_shells, idx_g2shells = init_Gshells( G2 )
  return GVectors(Ng, G, G2, idx_g2r, G2_shells, idx_g2shells)
end
\end{juliacode}

Implementation:
\begin{juliacode}
function Poisson_solve_fft( grid, gvec::GVectors, rho::Vector{Float64} )
  Npoints = grid.Npoints
  Nx = grid.Nx; Ny = grid.Ny; Nz = grid.Nz
  ctmp = zeros(ComplexF64,Nx,Ny,Nz)
  ctmp[:] = rho[:]
  # to reciprocal space
  fft!(ctmp)
  ctmp[1] = 0.0 + im*0.0
  for ig in 2:gvec.Ng
      ip = gvec.idx_g2r[ig]
      ctmp[ip] = 4.0*pi*ctmp[ip]/gvec.G2[ig]
  end
  # to real space
  ifft!(ctmp)
  return reshape(real(ctmp),Npoints)
end
\end{juliacode}



\section{Direct integration}

An alternative to solving Poisson equation is to integrate the equation defining
the Hartree potential directly. However a naive integration algorithm will scale
badly. In this section, we will adopt a direct integration method that is proposed
in \cite{Sundholm2005}. We will only describe the method for isolated system.
The extension to periodic system is described in \cite{Losilla2010}.

We will begin from the definition of Hartree potential:
\begin{equation}
V(\mathbf{r}_{1}) = \int_{-\infty}^{+\infty}
\rho(\mathbf{r}_{2}) \frac{1}{r_{12}} \, \mathrm{d}\mathbf{r}_{2}
\end{equation}

The Coulomb operator, $\frac{1}{r}$ can we written as the integral
\begin{equation}
\frac{1}{r} = \frac{2}{\sqrt{\pi}} \int_{0}^{\infty} e^{-r^2 t^2}\,\mathrm{d}t
\end{equation}
Using this identity, the Hartree potential is written as
\begin{equation}
V(\mathbf{r}_{1}) = \frac{2}{\sqrt{\pi}}
\int_{0}^{\infty}
\int_{-\infty}^{+\infty}
e^{-t^2(\mathbf{r}_1 - \mathbf{r}_2)^2} \rho(\mathbf{r}_2)
\, \mathrm{d}\mathbf{r}_{2}\mathrm{d}t
\end{equation}

By expanding the electron density in the following form
\begin{equation}
\rho(\mathbf{r}_2) = \sum_{\alpha\beta\gamma} d_{\alpha\beta\gamma}
\chi_{\alpha}(x_2) \chi_{\beta}(y_2) \chi_{\gamma}(z_2)
\end{equation}
the Hartree potential can be written as
\begin{multline}
V_{0}(x_{1},y_{1},z_{1}) = \frac{2}{\sqrt{\pi}}\sum_{\alpha_t} \omega_{\alpha_t}
\sum_{\alpha\beta\gamma} d_{\alpha\beta\gamma}
\int_{-\infty}^{\infty} e^{-t^2_{\alpha_t}(z_{1} - z_{2}) } \chi_{\gamma}(z_2)\mathrm{d}z_2
\times \int_{-\infty}^{\infty} e^{-t^2_{\alpha_t}(y_{1} - y_{2}) } \chi_{\beta}(y_2)\mathrm{d}y_2 \\
\times \int_{-\infty}^{\infty} e^{-t^2_{\alpha_t}(x_{1} - x_{2}) } \chi_{\alpha}(x_2)\mathrm{d}x_2
\end{multline}
where
$t_{\alpha_t}$ are integration points and $w_{\alpha_t}$ are the corresponding
integration weights.
By defining the following quantity:
\begin{equation}
F_{\gamma_x \alpha_t}^{x,\alpha_x} = \int_{-\infty}^{\infty}
e^{-t^2_{\alpha_t} (x_{\alpha_x} - x_2)^2} \chi_{\gamma_x}(x_2)\,\mathrm{d}x_2
\end{equation}
Hartree potential can be written as
\begin{equation}
V_{\alpha_x \alpha_y \alpha_z} = \frac{2}{\sqrt{\pi}} \sum_{\alpha_t} w_{\alpha_t}
\sum_{\gamma_z} F_{\gamma_z \alpha_z}^{z,\alpha_t}
\sum_{\gamma_y} F_{\gamma_y \alpha_y}^{y,\alpha_t}
\sum_{\gamma_x} F_{\gamma_x \alpha_x}^{x,\alpha_t}
d_{\gamma_x \gamma_y \gamma_z}
\end{equation}


