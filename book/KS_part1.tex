\chapter{Kohn-Sham equation part I}

In this chapter we will put together Schroedinger and Poisson solver
that we have built in the previous chapters to solve the Kohn-Sham equation:
\begin{equation}
\left[ -\frac{1}{2}\nabla^2 + V_{\mathrm{KS}}(\mathbf{r}) \right]
\psi_{i}(\mathbf{r}) = \epsilon_{i} \psi_{i}(\mathbf{r})
\end{equation}
where $V_{\mathrm{KS}}(\mathbf{r})$ is effective single particle potential or
the Kohn-Sham potential:
\begin{equation}
V_{\mathrm{KS}}(\mathbf{r}) =
V_{\mathrm{ext}}(\mathbf{r}) + V_{\mathrm{Ha}}(\mathbf{r}) + V_{\mathrm{xc}}(\mathbf{r})
\label{eq:KS_pot_local}
\end{equation}

The Kohn-Sham equation looks very much like Schroedinger equation,
but with two additional potentials: the Hartree and XC potential.
Hartree potential is the classical electrostatic interaction potential which is defined as
\begin{equation}
V_{\mathrm{Ha}}(\mathbf{r}) = \int \frac{\rho(\mathbf{r}')}{\left| \mathbf{r} - \mathbf{r}' \right|}
\,\mathrm{d}\mathbf{r}'
\end{equation}
where $\rho(\mathbf{r})$ is the electron density:
\begin{equation}
\rho(\mathbf{r}) = \sum_{i} f_{i} \psi_{i}^{*}(\mathbf{r}) \psi_{i}(\mathbf{r})
\label{eq:elec_dens_01}
\end{equation}
we can use any methods in Chapter \ref{chap:poisson_3d} to calculate
$V_{\mathrm{Ha}}(\mathbf{r})$. The factor $f_{i}$ in the Equation \ref{eq:elec_dens_01}
is the occupation number of the electron.
Exchange-correlation potential, $V_{\mathrm{xc}}$, has no explicit form in terms of
electron density. For practical purpose, we must resort to approximate forms which
will be given later.

Note that to calculate electron density, we must know the Kohn-Sham orbitals.
To obtain Kohn-Sham orbitals, we must solve the Kohn-Sham equations which
demand us to calculate $V_{\mathrm{Ha}}$ and $V_{\mathrm{xc}}$. Meanwhile,
we can not these potentials without knowing electron density.
Because of this chicken-and-egg
characteristic, the Kohn-Sham equation must be solved using self-consitent field (SCF) method.
We usually start from a guess or "input" electron density and solve the Kohn-Sham equation
for this guess density. From the solution of the Kohn-Sham equation, we obtain
Kohn-Sham energies and orbitals which can be used to calculate "output" electron density.
This "output" density is then compared with the "input" electron density. If they are
not the same then we create a new input density to the Kohn-Sham solver. This procedure
is repeated until the difference between "input" and "output" density is small.
In this case, we said that the SCF is convergent.

We also can monitor the convergence of other quantities other than electron density.
In this book, we will usually use total energy as the convergence criteria for SCF.
Thus, we will begin by reviewing total energy terms in a typical density functional
theory calculations.

\section{Total energy terms}

The main quantity of interest is total energy
\begin{equation}
E_{\mathrm{KS}} = E_{\mathrm{kin}} + E_{\mathrm{ext}}
+ E_{\mathrm{Ha}} + E_{\mathrm{xc}}
\end{equation}
%
The kinetic energy of noninteracting electrons:
\begin{equation}
E_{\mathrm{kin}} = -\frac{1}{2}\sum_{i} \int \psi_{i}^{*}(\mathbf{r}) \nabla^2 \psi_{i}(\mathbf{r})
\,\mathrm{d}\mathbf{r}
\end{equation}
%
External potential energy:
\begin{equation}
E_{\mathrm{ext}} = \int \rho(\mathbf{r}) V_{\mathrm{ext}}(\mathbf{r})
\,\mathrm{d}\mathbf{r}
\end{equation}
%
Hartree energy:
\begin{equation}
E_{\mathrm{Ha}} = \frac{1}{2} \int \rho(\mathbf{r}) V_{\mathrm{Ha}}(\mathbf{r})
\,\mathrm{d}\mathbf{r}
\end{equation}
%
XC energy (LDA):
\begin{equation}
E_{\mathrm{ext}} = \int \varepsilon_{\mathrm{xc}}[\rho(\mathbf{r})] \rho(\mathbf{r})
\,\mathrm{d}\mathbf{r}
\end{equation}
where $\varepsilon_{\mathrm{xc}}$ is the XC energy per particle per volume.

Alternative expression of total energy as sum of orbital energies:
\begin{equation}
E_{\mathrm{KS}} = \sum_{i}^{N} \epsilon_{i} - E_{\mathrm{Ha}} + E_{\mathrm{xc}}
- \int \frac{\delta E_{\mathrm{xc}}}{\delta \rho(\mathbf{r})} \rho(\mathbf{r})
\,\mathrm{d}\mathbf{r}
\end{equation}

Nucleus-nucleus interaction energy is also usually added to the Kohn-Sham energy
making up the total energy of the system:
\begin{equation}
E_{\mathrm{tot}} = E_{\mathrm{KS}} + E_{\mathrm{NN}}
\end{equation}
where:
\begin{equation}
E_{\mathrm{NN}} = \frac{1}{2} \sum_{I,J \neq I}
\frac{Z_{I} Z_{J}}{\left| \mathbf{R}_{I} - \mathbf{R}_{J} \right|}
\end{equation}



\section{Data structures}

We will first describe several custom data structure (or \jlinline{struct}s)
to make our program somewhat more manageable.

In the following, we will first consider the case of
$V_{\mathrm{xc}}=0$. This will make our first implementation
of self-consistent field to be easier.
We will consider a 3d systems, so the programs
that we have made in Chapter \ref{chap:sch_3d} will be used as the starting point
for building and Kohn-Sham solver.

\subsection{The Hamiltonian}

Because our Hamiltonian now contains additional potentials other that external ionic
potential, its action or multiplication to a wave function is
more complicated. We also need to implement several operations such as calculation
of electron density for given wave functions and total energy.
These calculations usually need to access to several variables. To make access to various
variables easy, we will wrap them in a "big" Julia struct. We will call this struct
as \jlinline{Hamiltonian}.

The definition of the \jlinline{Hamiltonian} struct can be seen in the following
Julia code.
\begin{fullwidth}
\begin{juliacode}
const AMG_PREC_TYPE = typeof( aspreconditioner(ruge_stuben(speye(1))) )
mutable struct Hamiltonian
    grid::Union{FD3dGrid,LF3dGrid}
    ∇2::SparseMatrixCSC{Float64,Int64}
    V_Ps_loc::Vector{Float64}
    V_Hartree::Vector{Float64}
    electrons::Electrons
    rhoe::Vector{Float64}
    atoms::Atoms
    precKin::Union{AMG_PREC_TYPE,ILU0Preconditioner}
    psolver::Union{PoissonSolverDAGE,PoissonSolverFFT}
    energies::Energies
    gvec::Union{Nothing,GVectors}
end
\end{juliacode}
\end{fullwidth}
%
A short explanation about the fields of \jlinline{Hamiltonian} follows.
\begin{itemize}
%
  \item \jlinline{grid}: the field that describes the real space grid points or
basis functions that is used to represent quantities such as wave functions,
potentials, and densities. In our case, this field is an instance of
\jlinline{FD3dGrid} or \jlinline{LF3dGrid}.
%
\item \jlinline{∇²}: the matrix representation of the ∇² operator
%
\item \jlinline{V_Ps_loc}: the local pseudopotential. It also will represent the
any external local potential that is felt by electrons.
Note that we have chosen the name \jlinline{V_Ps_loc} because of the
generalization that we will do in Chapter \ref{chap:ks_part_2}
%
\item \jlinline{V_Hartree}: the Hartree potential.
%
\item \jlinline{electrons}: an instance of type \jlinline{Electrons}. This field
stores various variables that are used to describe electron states such as number
of electrons, number of states, occupation numbers, etc.
%
\item \jlinline{rhoe}: the electron density
%
\item \jlinline{atoms}: an instance of \jlinline{Atoms}. This field can be used to represent
molecules, for example.
%
\item \jlinline{precKin}: the preconditioner based on the kinetic matrix. This preconditioner
can be for diagonalization of energy minimization.
%
\item \jlinline{psolver}: the Poisson equation solver which is used to calculate the Hartree
potential.
%
\item \jlinline{energies}: an instance of \jlinline{Energies}. This field stores
various total energy components.
%
\item \jlinline{gvec}: an instance of \jlinline{GVectors}. This field is only relevant for
periodic structure.
%
\end{itemize}


The following constructor can be used to initialize an instance of \jlinline{Hamiltonian}.
\begin{fullwidth}
\begin{juliacode}
function Hamiltonian( atoms::Atoms, grid, V_Ps_loc;
  Nelectrons=2, Nstates_extra=0, stencil_order=9,
  prec_type=:ILU0
)
  # Grid
  if typeof(grid) == FD3dGrid
      ∇2 = build_nabla2_matrix( grid, stencil_order=stencil_order )
  else
      ∇2 = build_nabla2_matrix( grid )
  end
  # Initialize gvec for periodic case
  if grid.pbc == (true,true,true)
      gvec = GVectors(grid)
  else
      gvec = nothing
  end
  Npoints = grid.Npoints
  V_Hartree = zeros(Float64, Npoints)
  Rhoe = zeros(Float64, Npoints)
  if prec_type == :amg
      precKin = aspreconditioner( ruge_stuben(-0.5*∇²) )
  else
      precKin = ILU0Preconditioner(-0.5*∇²)
  end
  electrons = Electrons( Nelectrons, Nstates_extra=Nstates_extra )
  if grid.pbc == (false,false,false)
      psolver = PoissonSolverDAGE(grid)
  else
      psolver = PoissonSolverFFT(grid)
  end
  energies = Energies()
  return Hamiltonian( grid, ∇², V_Ps_loc, V_Hartree, electrons,
                      Rhoe, atoms, precKin, psolver, energies, gvec )
end
\end{juliacode}
\end{fullwidth}

An important operation that must be defined for \jlinline{Hamiltonian} is the
multiplication between Hamiltonian and wave function.
This task is implemented in the function \jlinline{op_H}
\begin{juliacode}
function op_H( Ham::Hamiltonian, psi::Matrix{Float64} )
  Nbasis = size(psi,1)
  Nstates = size(psi,2)
  Hpsi = -0.5 * Ham.∇2 * psi
  for ist in 1:Nstates, ip in 1:Nbasis
      Hpsi[ip,ist] = Hpsi[ip,ist] + 
        ( Ham.V_Ps_loc[ip] + Ham.V_Hartree[ip] ) * psi[ip,ist]
  end
  return Hpsi
end
\end{juliacode}
In the function \jlinline{op_H}, first we apply the kinetic matrix by using the
\jlinline{∇²} field of \jlinline{Hamiltonian}, using sparse matrix
multiplication. This step is the followed by application of potential operator
which now consists of \jlinline{V_Ps_loc} and \jlinline{V_Hartree}.

We optionally can overload \jlinline{*} function so that we can use the usual
multiplication operator to carry out the action of Hamiltonian to wave function
\begin{juliacode}
import Base: *
function *( Ham::Hamiltonian, psi::Matrix{Float64} )
    return op_H(Ham, psi)
end
\end{juliacode}

We also define a function for updating the potentials (in this case only the Hartree potential, in
a full KS calculatio it also includes XC potential)
for an input electron density.
For our current purpose, this function do two things:
copy the input electron density to \jlinline{Ham.rhoe} and calculate the
Hartree potential by calling \jlinline{Poisson_solve} function.

\begin{juliacode}
function update!( Ham::Hamiltonian, Rhoe::Vector{Float64} )
    Ham.rhoe = Rhoe
    Ham.V_Hartree = Poisson_solve( Ham.psolver, Ham.grid, Rhoe )
    return
end
\end{juliacode}


\subsection{Electrons}

The type \jlinline{Electrons} stores several variables related to single-electron states.
\begin{juliacode}
mutable struct Electrons
    Nelectrons::Int64
    Nstates::Int64
    Nstates_occ::Int64
    Focc::Array{Float64,1}
    ene::Array{Float64,1}
end
\end{juliacode}

Electron density calculation \ref{eq:elec_dens_01}:
\begin{juliacode}
function calc_rhoe( Ham, psi::Array{Float64,2} )
    Nbasis = size(psi,1)
    Nstates = size(psi,2)
    Rhoe = zeros(Float64,Nbasis)
    for ist in 1:Nstates, ip in 1:Nbasis
        Rhoe[ip] = Rhoe[ip] +
          Ham.electrons.Focc[ist]*psi[ip,ist]*psi[ip,ist]
    end
    return Rhoe
end
\end{juliacode}


\subsection{Total energy terms}

We introduce the \jlinline{Energies} type to store various energy terms.
\begin{juliacode}
mutable struct Energies
    Kinetic::Float64
    Ps_loc::Float64
    Hartree::Float64
    NN::Float64
end
\end{juliacode}

Overload sum:
\begin{juliacode}
import Base: sum
function sum( ene::Energies )
    return ene.Kinetic + ene.Ps_loc + ene.Hartree + ene.NN
end
\end{juliacode}

Calculation of the kinetic energy:
\begin{equation}
E_{\mathrm{kin}} =
-\frac{1}{2} \int \psi_{i}(\mathbf{r}) \nabla^{2} \psi_{i}(\mathbf{r})\,\mathrm{d}\mathbf{r}
\end{equation}
can be done using the following function.
\begin{fullwidth}
\begin{juliacode}
function calc_E_kin( Ham::Hamiltonian, psi::Array{Float64,2} )
    Nbasis = size(psi,1)
    Nstates = size(psi,2)
    E_kin = 0.0
    nabla2psi = zeros(Float64,Nbasis)
    dVol = Ham.grid.dVol
    for ist in 1:Nstates
        @views nabla2psi = -0.5*Ham.∇2*psi[:,ist]
        @views E_kin = E_kin + Ham.electrons.Focc[ist]*dot(psi[:,ist],nabla2psi[:])*dVol
    end
    return E_kin
end
\end{juliacode}
\end{fullwidth}

In the function \jlinline{calc_energies!}, we calculate total electronic energy terms.
This function modifies \jlinline{Ham.energies}.
\begin{juliacode}
function calc_energies!( Ham::Hamiltonian, psi::Array{Float64,2} )
  dVol = Ham.grid.dVol
  Ham.energies.Kinetic = calc_E_kin( Ham, psi )
  Ham.energies.Ps_loc = sum( Ham.V_Ps_loc .* Ham.rhoe )*dVol
  Ham.energies.Hartree = 0.5*sum( Ham.V_Hartree .* Ham.rhoe )*dVol
  return
end
\end{juliacode}
It is sometimes convenient to return the energies directly. This is done by
the function \jlinline{calc_energies} which just wraps \jlinline{calc_energies!}
function.
\begin{juliacode}
function calc_energies(Ham, psi)
  calc_energies!(Ham, psi)
  return Ham.energies
end
\end{juliacode}


\section{Examples Hartree theory calculations}

In this section, we will begin our implementation of SCF Hatree theory for several
simple systems.
As in the previous chapters, we will start fromm a system with 3d harmonic potential as the
external potential. We will also 

\subsection{Harmonic potential}

First, we define the grid.
\begin{juliacode}
AA = [-3.0, -3.0, -3.0]
BB = [3.0, 3.0, 3.0]
NN = [25, 25, 25]
grid = FD3dGrid( NN, AA, BB )
\end{juliacode}

The, we calculate the external potential. We will use \jlinline{V_Ps_loc} as the name of
the external potential even though we are not dealing with pseudopotential.
We also choose $\omega=2$.
\begin{juliacode}
V_Ps_loc = pot_harmonic( grid, ω=2 )
\end{juliacode}

The next step is to initialize an instance of \jlinline{Hamiltonian}. We need to specify
number of electrons and number of states for our electronic states. Here we choose to 8
electrons and 4 states, each states is doubly occupied.
\begin{juliacode}
Nelectrons = 8
Nstates = round(Int64,Nelectrons/2)
Ham = Hamiltonian( Atoms(), grid, V_Ps_loc, Nelectrons=Nelectrons )
\end{juliacode}

After that, we prepare random wave functions for guess solution to the Kohn-Sham equation.
\begin{juliacode}
Npoints = grid.Npoints
dVol = grid.dVol
psi = rand(Float64,Npoints,Nstates)
ortho_sqrt!(psi)
psi = psi/sqrt(dVol)
\end{juliacode}
and calculate the electron density associated with these wave functions.
We also printed the integrated electron density, approximated with simple summation.
Note that the integrated electron density should be close the the number of electrons.
\begin{juliacode}
Rhoe = calc_rhoe( Ham, psi )
@printf("Integrated Rhoe = %18.10f\n", sum(Rhoe)*dVol)
\end{juliacode}

From the guess electron density, we update the Hamiltonian, calculate the
Hartree potential by calling the \jlinline{update!} function.
\begin{juliacode}
update!( Ham, Rhoe )
\end{juliacode}

We also calculate total energy for our initial guess wave functions and electron
density.
\begin{juliacode}
Etot = sum( calc_energies( Ham, psi ) )
@printf("Initial Etot = %18.10f\n", Etot)
\end{juliacode}

Before entering the SCF cycle, we need to prepare and define several variables
which will be used in the SCF cycle.
\begin{juliacode}
evals = zeros(Float64,Nstates)
Etot_old = Etot
dEtot = 0.0
dRhoe = 0.0
betamix = 0.5
NiterMax = 100
\end{juliacode}
An important variable that needs attention is \jlinline{betamix}. This variable
plays the role of $\beta$ in the following equation.
\begin{equation}
\rho^{i+1}_{\mathrm{in}}(\mathbf{r}) = \beta\rho(\mathbf{r})^{i}_{\mathrm{out}} +
(1 - \beta)\rho^{i}_{\mathrm{in}}(\mathbf{r})
\label{eq:linear_mix_rhoe}
\end{equation}
where $0 < \beta <= 1$.
In the Equation \eqref{eq:linear_mix_rhoe}, $\rho^{i+1}_{\mathrm{in}}(\mathbf{r})$
is the input density for the next SCF iteration.

The SCF cycle is implemented in the following Julia code.
\begin{juliacode}
for iterSCF in 1:NiterMax
  # Diagonalize the Hamiltonian
  evals = diag_LOBPCG!(Ham, psi, Ham.precKin)
  psi = psi/sqrt(dVol)
  # Calculate output electron density
  Rhoe_new = calc_rhoe(Ham, psi)
  # Mix electron density
  Rhoe = betamix*Rhoe_new + (1-betamix)*Rhoe
  # Update potential
  update!( Ham, Rhoe )
  # Calculate total energy
  Etot = sum( calc_energies( Ham, psi ) )
  dRhoe = sum(abs.(Rhoe - Rhoe_new))/Npoints
  dEtot = abs(Etot - Etot_old)
  @printf("%5d %18.10f %18.10e %18.10e\n", iterSCF, Etot, dEtot, dRhoe)
  # Check convergence
  if dEtot < 1e-6
    @printf("Convergence is achieved in %d iterations\n", iterSCF)
    for i in 1:Nstates
      @printf("%3d %18.10f\n", i, evals[i])
    end
    break
  end
  Etot_old = Etot
end
\end{juliacode}

The result of the SCF:
\begin{textcode}
... snipped
  33      62.2907110243   1.4097586885e-06   5.3155705598e-08
  34      62.2907100832   9.4107285520e-07   4.5940110326e-08
Convergence is achieved in 34 iterations
 1       9.8038626839
 2      11.1505190947
 3      11.1505191402
 4      11.1505192492
----------------------------
Total energy components
----------------------------
Kinetic =      12.9807771382
Ps_loc  =      25.0898019726
Hartree =      24.2201309724
NN      =       0.0000000000
----------------------------
Total   =      62.2907100832
\end{textcode}

Here is the result obtained using Octopus.
\begin{textcode}
 *********************** SCF CYCLE ITER #   18 ************************
 etot  =  6.22907491E+01 abs_ev   =  1.22E-04 rel_ev   =  1.41E-06
 ediff =       -1.13E-04 abs_dens =  6.72E-05 rel_dens =  8.39E-06
Matrix vector products:     27
Converged eigenvectors:      0

#  State  Eigenvalue [H]  Occupation    Error
      1        9.803876    2.000000   (9.3E-06)
      2       11.150521    2.000000   (9.3E-06)
      3       11.150524    2.000000   (7.1E-06)
      4       11.150525    2.000000   (9.6E-06) 
\end{textcode}

We get similar result with Octopus.



\subsection{H atom}

A next example that we will try is hydrogen atom. Here is our setup.
\begin{juliacode}
AA = [-8.0, -8.0, -8.0]
BB = [ 8.0,  8.0,  8.0]
NN = [41, 41, 41]
grid = FD3dGrid( NN, AA, BB )
atoms = Atoms( xyz_string=
  """
  1

  H  0.0  0.0  0.0
  """ )
V_Ps_loc = pot_Hps_HGH(atoms, grid)
Nstates = 1
Nelectrons = 1
Ham = Hamiltonian(atoms, grid, V_Ps_loc, Nelectrons=1)
\end{juliacode}

Result:
\begin{textcode}
... snipped
14      -0.2562610358   2.0600214368e-06   1.9590484379e-08
15      -0.2562602740   7.6184773828e-07   9.7155909123e-09
Convergence is achieved in 15 iterations
1      -0.0428298539
----------------------------
Total energy components
----------------------------
Kinetic =       0.2928408518
Ps_loc  =      -0.7625329979
Hartree =       0.2134318721
NN      =       0.0000000000
----------------------------
Total   =      -0.2562602740
\end{textcode}


{\center
\includegraphics[width=\textwidth]{../codes/hartree_scf/LOG_files/IMG_H.pdf}
\par}




\subsection{$\ce{H2}$ molecule}


Using the same grid as we have used for hydrogen atom.

\begin{juliacode}
atoms = Atoms( xyz_string=
  """
  2
  
  H   0.75  0.0  0.0
  H  -0.75  0.0  0.0
  """, in_bohr=true)
V_Ps_loc = pot_Hps_HGH(atoms, grid)
Nstates = 1
Nelectrons = 2
Ham = Hamiltonian(atoms, grid, V_Ps_loc, Nelectrons=Nelectrons)
\end{juliacode}

We need to calculate ion-ion interaction energy. Do this before SCF cycle.
\begin{juliacode}
Ham.energies.NN = calc_E_NN( [1.0, 1.0], atoms.positions )
\end{juliacode}

Result:
\begin{textcode}
... snipped
16      -0.5972202712   1.3097904592e-06   1.0969070600e-08
17      -0.5972197417   5.2951699026e-07   5.4868596190e-09
Convergence is achieved in 17 iterations
1      -0.1083633271
----------------------------
Total energy components
----------------------------
Kinetic =       0.8229371585
Ps_loc  =      -3.1339854624
Hartree =       1.0471618956
NN      =       0.6666666667
----------------------------
Total   =      -0.5972197417
\end{textcode}

\begin{figure}[h]
\begin{center}
\includegraphics[width=\textwidth]{../codes/hartree_scf/LOG_files/IMG_H2.pdf}
\end{center}
\caption{Caption XXX}
\end{figure}




\section{Kohn-Sham calculations}

We are now ready to include the XC term.

\begin{equation}
E_{\mathrm{xc}}[\rho(\mathbf{r})] = \int \rho(\mathbf{r})
\varepsilon_{xc}(\rho(\mathbf{r}))\,\mathrm{d}\mathbf{r}
\end{equation}

Decomposition:
\begin{equation}
E_{xc} = E_{x} + E_{c}
\end{equation}

\subsection{Exchange term: Slater exchange}

Homogeneous electron, exchange energy:
\begin{equation}
E_{x}[\rho(\mathbf{r})] = -\frac{3}{4} \left(\frac{3}{\pi}\right)^{1/3}
\int \rho(\mathbf{r})^{4/3}\,\mathrm{d}\mathbf{r}
\end{equation}

Or can be written as:
\begin{equation}
E_{x}[\rho(\mathbf{r})] = \int \rho(\mathbf{r}) \varepsilon_{x}(\rho(\mathbf{r}))\,\mathrm{d}\mathbf{r}
\end{equation}

Exchange energy density:
\begin{equation}
\varepsilon_{x}(\rho) = C_{x}\rho^{1/3}
\end{equation}
with parameter:
\begin{equation}
C_{x} = \frac{3}{4}\left(\frac{3}{\pi}\right)^{1/3}  
\end{equation}


\subsection{Correlation VWN}

VWN parameterization:
\begin{equation}
\varepsilon_{c} = A \left\{
\mathrm{ln}\frac{x}{X(x)} + \frac{2b}{Q}\mathrm{atan}\frac{Q}{2x+b}
- \frac{bx_{0}}{X(x_{0})}\left[
\mathrm{ln}\frac{(x - x_0)^2}{X(x)} + \frac{2(b + 2x_0)}{Q}\mathrm{atan}\frac{Q}{2x + b}
\right]
\right\}
\end{equation}

\begin{align}
x & = \sqrt{r_{s}} \\
r_s & = \left( \frac{3}{4\pi\rho} \right)^{1/3} \\
X(x) & = x^2 + bx + c \\
Q & = \sqrt{4c - b^2}
\end{align}

Parameter:
\begin{align}
A & = 0.0310907 \\
b & = 3.72744 \\
c & = 12.9352 \\
x_0 & = -0.10498
\end{align}

Wigner-Seitz radius is defined by:
\begin{equation}
\frac{4}{3}\pi r^{3}_{s} = \frac{1}{\rho}
\end{equation}

Potential:
\begin{equation}
V^{LDA}_{xc} = \frac{\delta E^{LDA}_{xc}}{\delta \rho(\mathbf{r})} = 
\varepsilon_{\mathrm{xc}}( \rho(\mathbf{r}) ) + \rho(\mathbf{r})
\frac{\partial \varepsilon_{\mathrm{xc}}( \rho(\mathbf{r}) )}{\partial\rho(\mathbf{r})}
\end{equation}


\subsection{Implementation}

New Hamiltonian, include \jlinline{V_XC}.

Update the potential:

\begin{juliacode}
function update!( Ham::Hamiltonian, Rhoe::Vector{Float64} )
  Ham.rhoe = Rhoe
  Ham.V_Hartree = Poisson_solve( Ham.psolver, Ham.grid, Rhoe )
  Ham.V_XC = calc_Vxc_VWN( XCCalculator(), Rhoe )
  return
end
\end{juliacode}

Application of Hamiltonian

\begin{juliacode}
function op_H( Ham::Hamiltonian, psi::Matrix{Float64} )
  Nbasis = size(psi,1)
  Nstates = size(psi,2)
  Hpsi = zeros(Float64,Nbasis,Nstates)
  Hpsi = -0.5*Ham.∇² * psi
  for ist in 1:Nstates, ip in 1:Nbasis
    Hpsi[ip,ist] = Hpsi[ip,ist] + ( Ham.V_Ps_loc[ip] + Ham.V_Hartree[ip] +
                   Ham.V_XC[ip] ) * psi[ip,ist]
  end
  return Hpsi
end
\end{juliacode}

Calculation of XC energy:
\begin{juliacode}
epsxc = calc_epsxc_VWN( XCCalculator(), Ham.rhoe )
Ham.energies.XC = sum( epsxc .* Ham.rhoe )*dVol
\end{juliacode}

\section{Examples of KS calculations}

\subsection{Harmonic potential}

Setup:
\begin{juliacode}
AA = [-3.0, -3.0, -3.0]
BB = [3.0, 3.0, 3.0]
NN = [25, 25, 25]
grid = FD3dGrid( NN, AA, BB )
V_Ps_loc = pot_harmonic( grid, ω=2 )
Nelectrons = 8
Nstates = round(Int64,Nelectrons/2)
Ham = Hamiltonian( Atoms(), grid, V_Ps_loc, Nelectrons=Nelectrons )
\end{juliacode}

\begin{textcode}
... snipped
  19      57.5543743808   1.0560459387e-06   4.0259990163e-05
  20      57.5543736562   7.2459251044e-07   3.1156747100e-05
Convergence is achieved in 20 iterations

Eigenvalues:
 1       9.0567277414
 2      10.4655966931
 3      10.4655970965
 4      10.4655977729
----------------------------
Total energy components
----------------------------
Kinetic =      13.6600637584
Ps_loc  =      23.8423941552
Hartree =      24.8463331194
XC      =      -4.7944173767
NN      =       0.0000000000
----------------------------
Total   =      57.5543736562
\end{textcode}


Result from Octopus:
\begin{fullwidth}
\begin{textcode}
*********************** SCF CYCLE ITER #   17 ************************
 etot  =  5.75543034E+01 abs_ev   =  4.97E-05 rel_ev   =  6.14E-07
 ediff =       -5.09E-05 abs_dens =  6.73E-05 rel_dens =  8.42E-06
Matrix vector products:     31
Converged eigenvectors:      0

#  State  Eigenvalue [H]  Occupation    Error
      1        9.056759    2.000000   (9.9E-06)
      2       10.465603    2.000000   (1.2E-05)
      3       10.465607    2.000000   (5.9E-06)
      4       10.465608    2.000000   (7.1E-06) 
\end{textcode}
\end{fullwidth}

Our result is similar to Octopus result.


\subsection{Hydrogen atom}

Result:
\begin{textcode}
  17      -0.4697222378   2.0241354490e-06   4.7656241007e-09
  18      -0.4697214473   7.9055452079e-07   2.4859509631e-09
Convergence is achieved in 18 iterations

Eigenvalues:
 1      -0.2449501107
----------------------------
Total energy components
----------------------------
Kinetic =       0.5027978737
Ps_loc  =      -1.0263363191
Hartree =       0.2995317605
XC      =      -0.2457147624
NN      =       0.0000000000
----------------------------
Total   =      -0.4697214473
\end{textcode}

Octopus result:
\begin{fullwidth}
\begin{textcode}
*********************** SCF CYCLE ITER #   12 ************************
 etot  = -4.70593023E-01 abs_ev   =  8.06E-07 rel_ev   =  3.28E-06
 ediff =       -4.65E-11 abs_dens =  9.21E-06 rel_dens =  9.21E-06
Matrix vector products:      6
Converged eigenvectors:      0
 
 #  State  Eigenvalue [H]  Occupation    Error
       1       -0.245334    1.000000   (3.1E-06)
\end{textcode}
\end{fullwidth}

Quite similar to Octopus.
Difference probabably due to the Poisson solver.

Need to study convergence with respect to grid spacing.

{\centering
\includegraphics[width=\textwidth]{../codes/ks_dft_02/LOG_files/IMG_H.pdf}
}

\subsection{$\ce{H2}$ molecule}

Result:
\begin{textcode}
  15      -1.1975841508   4.0237235348e-06   3.8822972895e-08
  16      -1.1975850469   8.9614081089e-07   2.1763673163e-08
Convergence is achieved in 16 iterations

Eigenvalues:
 1      -0.3827444348
----------------------------
Total energy components
----------------------------
Kinetic =       1.1958829137
Ps_loc  =      -3.7020236680
Hartree =       1.3006817627
XC      =      -0.6587927219
NN      =       0.6666666667
----------------------------
Total   =      -1.1975850469
\end{textcode}

Octopus result:
\begin{fullwidth}
\begin{textcode}
********************** SCF CYCLE ITER #   12 ************************
 etot  = -1.19927504E+00 abs_ev   =  1.50E-06 rel_ev   =  1.95E-06
 ediff =        1.68E-06 abs_dens =  2.11E-06 rel_dens =  1.06E-06
Matrix vector products:     15
Converged eigenvectors:      1

#  State  Eigenvalue [H]  Occupation    Error
      1       -0.383095    2.000000   (6.0E-07) 
\end{textcode}
\end{fullwidth}

Similar observation as H atom.

{\centering
\includegraphics[width=\textwidth]{../codes/ks_dft_02/LOG_files/IMG_H2.pdf}
}

\section{Exercise}

SCF convergence. Vary the \jlinline{betamix} parameter. Observe or the convergence
of SCF with respect to betamix.

Try adding one empty orbitals. Visualize the orbitals. Compare the results of
KS and Hartree. Are there any difference?

Calculate KS for periodic H-chain.